buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2'
    }
}

plugins {
    id "org.sonarqube" version "2.6.2"
}

apply plugin: 'java'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'jacoco'

group = 'de.kgrupp'
def versionStr = System.properties['inoks.java.utils.version']
version = !versionStr ? "LOCAL-VERSION" : versionStr

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'de.kgrupp:inoks-java-monads:0.5.2.13'

    compile group: 'org.jetbrains', name: 'annotations', version: '15.0'

    compile 'javax.xml.bind:jaxb-api:2.2.11'
    compile 'com.sun.xml.bind:jaxb-core:2.2.11'
    compile 'com.sun.xml.bind:jaxb-impl:2.2.11'
    compile 'javax.activation:activation:1.1.1'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.0.1'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.4.0'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda', version: '2.9.6'

    compileOnly 'org.projectlombok:lombok:1.18.0'
    testCompileOnly 'org.projectlombok:lombok:1.18.0'

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.2.0'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.2.0'
}

allprojects {
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test) {
        systemProperty "file.encoding", "UTF-8"
    }
}

test {
    useJUnitPlatform()
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'inoks-java-utils'
        property 'sonar.projectName', 'inoks-java-utils'
    }
}

modifyPom {
    project {
        name 'inoks-java-utils'
        description 'A set of java classes which are helpful for developing with java 8. Helpers for inoks-java-monads.'
        url 'https://github.com/kgrupp/inoks-java-utils'
        inceptionYear '2017'

        scm {
            url 'https://github.com/kgrupp/inoks-java-utils'
            connection 'scm:https://kgrupp@github.com/kgrupp/inoks-java-utils.git'
            developerConnection 'scm:git://github.com/kgrupp/inoks-java-utils.git'
        }

        licenses {
            license {
                name 'MIT License'
                url 'https://github.com/kgrupp/inoks-java-utils/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'kgrupp'
                name 'Konstantin Grupp'
                email 'coding@konstantin-grupp.de'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = project.hasProperty('nexusUrlRelease') ? nexusUrlRelease : 'http://localhost:8081/repository/maven-releases/'
    snapshotRepositoryUrl = project.hasProperty('nexusUrlSnapshot') ? nexusUrlSnapshot : 'http://localhost:8081/repository/maven-snapshots'
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}